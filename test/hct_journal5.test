# 2023 June 30
#
# The author disclaims copyright to this source code.  In place of
# a legal notice, here is a blessing:
#
#    May you do good and not evil.
#    May you find forgiveness for yourself and forgive others.
#    May you share freely, never taking more than you give.
#
#*************************************************************************
#

set testdir [file dirname $argv0]
source $testdir/tester.tcl
source $testdir/hct_common.tcl
set testprefix hct_journal5


# Setup for logging 
db close
sqlite3_shutdown
sqlite_log_to_stdout

hct_reset_db
catch_hct_journal_init db
sqlite3_hct_journal_setmode db LEADER

proc do_perm_test {tn} {
  forcedelete test.db2

  # Initialize test.db2 as a new, empty, replication enabled db.
  sqlite3 db2 file:test.db2?hctree=1 -uri 1
  catch_hct_journal_init db2
  db2 close
  hct_copy_db test.db2 bak.db

  db eval {
      SELECT group_concat(cid, ' ') AS lCid
      FROM hct_journal 
      GROUP BY snapshot 
      ORDER BY snapshot
  } {
    set state ""
    foreach-perm lTrans $lCid {
      hct_copy_db bak.db test.db2
      sqlite3 db2 test.db2
      sqlite3_hct_journal_setmode db2 FOLLOWER

      foreach cid $lTrans {
        db eval {SELECT query, snapshot FROM hct_journal WHERE cid=$cid} {}
        db2 eval BEGIN
        db2 eval $query
        set rc [sqlite3_hct_journal_follower_commit db2 $query $cid $snapshot]
      }

      if {$state==""} {
        set state [hct_db_contents db2]
      } else {
        do_test $tn.($lCid)->($lTrans) { hct_db_contents db2 } $state
      }

      db2 close
    }
    hct_copy_db test.db2 bak.db
  }
}

hct_write_leader db {
  CREATE TABLE t1(a INTEGER PRIMARY KEY, b TEXT);
  INSERT INTO t1 VALUES(1, 'one');
}

hct_write_leader db { INSERT INTO t1 VALUES(2, 'two'); }
hct_write_leader db { INSERT INTO t1 VALUES(3, 'three'); }
hct_write_leader db { INSERT INTO t1 VALUES(4, 'four'); }

hct_write_leader db { 
  UPDATE t1 SET b='eleven' WHERE a=3
}
hct_write_leader db { 
  INSERT INTO t1 VALUES(5, 'five');
}


do_perm_test 1

# Argument lList must be a list where each element is itself a list
# of two integers - a CID and a snapshot value - in that order.
# 
proc foreach-dep-perm {var lList body} {
  uplevel [list foreach-dep-perm-r $var {} $lList $body]
}

proc foreach-dep-perm-r {var lPrefix lList body} {
  if {[llength $lList]==0} {
    upvar $var myvar
    set myvar $lPrefix
    uplevel $body
  } else {
    array set D [list]
    foreach done $lPrefix {
      foreach {cid snap} $done break
      set D($cid) 1
    }

    for {set avail 0} {[info exists D([expr $avail+1])]} {incr avail} {}

    for {set ii 0} {$ii < [llength $lList]} {incr ii} {
      set entry [lindex $lList $ii]
      foreach {cid snap} $entry break
      if {$snap<=$avail} {
        set lNew [lreplace $lList $ii $ii]
        set lNewPrefix [concat $lPrefix [list $entry]]
        uplevel [list foreach-dep-perm-r $var $lNewPrefix $lNew $body]
      }
    }
  }
}


proc do_perm_test2 {tn} {
  db function list list
  set tn2 0
  foreach-dep-perm lEntry [
    db eval {SELECT list(cid, snapshot, query) FROM hct_journal}
  ] {
  
    #puts -nonewline "lEntry: "
    #foreach e $lEntry { puts -nonewline "[lindex $e 0]/[lindex $e 1] " }
    #puts ""
  
    incr tn2
    forcedelete test.db2
    sqlite3 db2 file:test.db2?hctree=1 -uri 1
    catch_hct_journal_init db2
    sqlite3_hct_journal_setmode db2 FOLLOWER
  
    set tn3 0
    foreach entry $lEntry {
      incr tn3
      foreach {cid snap query} $entry {}
      #puts "cid=$cid snap=$snap"
      db2 eval BEGIN
      db2 eval $query
      set rc [sqlite3_hct_journal_follower_commit db2 $query $cid $snap]
      do_test $tn.$tn2.$tn3 [list set {} $rc] SQLITE_OK
    }

    db2 close
    sqlite3 db2 test.db2
    do_test $tn.$tn2.data {
      hct_db_contents db2
    } [hct_db_contents db]
  }

}

do_perm_test2 2

#-------------------------------------------------------------------------
hct_reset_db
catch_hct_journal_init db
sqlite3_hct_journal_setmode db LEADER

hct_write_leader db {
  CREATE TABLE t1(a INTEGER PRIMARY KEY, b TEXT);
  INSERT INTO t1 VALUES(1, 'one');
  INSERT INTO t1 VALUES(2, 'two');
}

hct_write_leader db { INSERT INTO t1 VALUES(3, 'three'); }
hct_write_leader db { UPDATE t1 SET b='eleven' WHERE a=1 }
hct_write_leader db { INSERT INTO t1 VALUES(4, 'four'); }
hct_write_leader db { INSERT INTO t1 VALUES(0, 'zero'); }
hct_write_leader db { DELETE FROM t1 }
hct_write_leader db { INSERT INTO t1 VALUES(11, 'eleven') }
hct_write_leader db { UPDATE t1 SET b=b||b; }
hct_write_leader db { CREATE TABLE t2(a, b) }
hct_write_leader db { INSERT INTO t2 VALUES('a', 'b'); }

do_perm_test2 3

# execsql_pp { SELECT cid, snapshot, query FROM hct_journal; }
#-------------------------------------------------------------------------
hct_reset_db
catch_hct_journal_init db
sqlite3_hct_journal_setmode db LEADER

hct_write_leader db {
  CREATE TABLE t1(a INTEGER PRIMARY KEY, b TEXT);
  CREATE UNIQUE INDEX t1b ON t1(b);
}

hct_write_leader db {
  INSERT INTO t1 VALUES(1, 'val1');
}
hct_write_leader db { DELETE FROM t1 WHERE a=1; }
hct_write_leader db { INSERT INTO t1 VALUES(2, 'val1'); }
hct_write_leader db { DELETE FROM t1 WHERE a=2; }
hct_write_leader db { INSERT INTO t1 VALUES(3, 'val1'); }

hct_write_leader db { INSERT INTO t1 VALUES(4, 'val2'); }
hct_write_leader db { CREATE TABLE t2(x PRIMARY KEY, y UNIQUE); }
hct_write_leader db { INSERT INTO t2 VALUES('a', 'b'); }
hct_write_leader db { DELETE FROM t2; }
hct_write_leader db { INSERT INTO t2 VALUES('b', 'b'); }

do_perm_test2 4

finish_test

