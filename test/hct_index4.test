# 2022 November 16
#
# The author disclaims copyright to this source code.  In place of
# a legal notice, here is a blessing:
#
#    May you do good and not evil.
#    May you find forgiveness for yourself and forgive others.
#    May you share freely, never taking more than you give.
#
#*************************************************************************
# This file implements regression tests for SQLite library.
#

set testdir [file dirname $argv0]
source $testdir/tester.tcl
set testprefix hct_index4

# Setup for logging 
db close
sqlite3_shutdown
sqlite_log_to_stdout

expr srand(0)
#set hct_extra_write_logging 1

reset_db
execsql { PRAGMA journal_mode = wal }

hct_reset_db

proc rand {min max} {
  expr {$min + int(abs(rand() * (1 + $max - $min)))}
}
db func rand rand


set nRow [expr 100]
set nTrans 2000

set lQuery [list                         \
  "SELECT a, b FROM tbl ORDER BY a"      \
  "SELECT a, b FROM tbl ORDER BY a DESC" \
  "SELECT a, b FROM tbl ORDER BY b"      \
  "SELECT a, b FROM tbl ORDER BY b DESC" \
  "SELECT a, b FROM tbl WHERE b>($nRow/5) AND b<($nRow*4/5) ORDER BY b" \
  "SELECT a, b FROM tbl WHERE b>($nRow/5) AND b<($nRow*4/5) ORDER BY b DESC" \
]

do_execsql_test 1.0 {
  PRAGMA page_size = 512;
  CREATE TABLE tbl(
    a INTEGER PRIMARY KEY,
    b INTEGER
  );
  CREATE INDEX idx ON tbl(b);

  WITH s(i) AS (
    SELECT 1 UNION ALL SELECT i+1 FROM s WHERE i<$nRow
  )
  INSERT INTO tbl SELECT i, i FROM s;
}

for {set ii 0} {$ii < $nTrans} {incr ii} {

  execsql {
    BEGIN;
      UPDATE tbl SET b=rand(1, $nRow) WHERE a=rand(1, $nRow);
      UPDATE tbl SET b=rand(1, $nRow) WHERE a=rand(1, $nRow);
      UPDATE tbl SET b=rand(1, $nRow) WHERE a=rand(1, $nRow);
    COMMIT;
  }

  set cmd "db_$ii"
  sqlite3 $cmd test.db

  $cmd eval { BEGIN; }
  foreach q $lQuery {
    lappend R($cmd) [$cmd eval $q]
  }
}

puts [time {
for {set ii 0} {$ii < $nTrans} {incr ii} {
  set cmd "db_$ii"

  set tn 0
  foreach q $lQuery res $R($cmd) {
    do_execsql_test -db $cmd 1.$cmd.$tn $q $res
    incr tn
  }

  $cmd close
  unset R($cmd)
}
}]

set lCmd [list]
for {set ii 0} {$ii < 2000} {incr ii} {
  execsql {
    BEGIN;
      UPDATE tbl SET b=rand(1, $nRow) WHERE a=rand(1, $nRow);
      UPDATE tbl SET b=rand(1, $nRow) WHERE a=rand(1, $nRow);
      UPDATE tbl SET b=rand(1, $nRow) WHERE a=rand(1, $nRow);
    COMMIT;
  }

  set cmd "db_$ii"
  sqlite3 $cmd test.db

  $cmd eval { BEGIN; }
  foreach q $lQuery {
    lappend R($cmd) [$cmd eval $q]
  }
  
  lappend lCmd $cmd
  if {[llength $lCmd]==40} {
    foreach cmd $lCmd {
      set tn 0
      foreach q $lQuery res $R($cmd) {
        do_execsql_test -db $cmd 2.$ii.$cmd.$tn $q $res
        incr tn
      }
    }

    foreach cmd [lrange $lCmd 0 9] {
      $cmd close
      unset R($cmd)
    }
    set lCmd [lrange $lCmd 10 end]
  }
}
foreach cmd $lCmd {
  $cmd close
  unset R($cmd)
}

#execsql_pp { SELECT * FROM tbl }
#execsql_pp { SELECT * FROM hcttmap }
#show_pagemap
#show_all_entries

finish_test

