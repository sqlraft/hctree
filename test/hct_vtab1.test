# 2017 July 14
#
# The author disclaims copyright to this source code.  In place of
# a legal notice, here is a blessing:
#
#    May you do good and not evil.
#    May you find forgiveness for yourself and forgive others.
#    May you share freely, never taking more than you give.
#
#*************************************************************************
# This file implements regression tests for SQLite library.  The
# focus of this script is testing the lsm1 virtual table module.
#

set testdir [file dirname $argv0]
source $testdir/tester.tcl
set testprefix hct_vtab1

load_static_extension db hct

do_execsql_test 1.1.0 {
  CREATE VIRTUAL TABLE x1 USING hct;
  PRAGMA table_info(x1);
} {
  0 k INTEGER 0 {} 0 
  1 v BLOB 0 {} 0
}

do_execsql_test 1.1.1 {
  INSERT INTO x1 VALUES(1, 'abc');
}

do_execsql_test 1.1.2 {
  SELECT * FROM x1;
} {1 abc}

do_execsql_test 1.1.3 {
  INSERT INTO x1 VALUES(12, 'def');
}

do_execsql_test 1.1.4 {
  SELECT * FROM x1;
} {1 abc 12 def}

do_execsql_test 1.1.5 {
  INSERT INTO x1 VALUES(25, 'ghi');
}

do_execsql_test 1.1.6 {
  SELECT * FROM x1;
} {1 abc 12 def 25 ghi}

foreach {tn seq seq2} {
  0 {1 2} {1}
  1 {1 2} {2}
  2 {11 5 4 10 7 8} 11
  3 {932 478 735} {}
  4 {0 7 4 6} {4}
  5 {7 6 3 0} 7
  6 {6 10 11 9 5 1} 9
  7 {0 19 0 9 2 18 10 16 19 16} {10 19 16 18 0 2}
} {
  do_execsql_test 1.2.$tn.0 {
    DROP TABLE IF EXISTS x1;
    CREATE VIRTUAL TABLE x1 USING hct;
  }

  foreach k $seq {
    do_test 1.2.$tn.1.$k {
      execsql { REPLACE INTO x1 VALUES($k, 'hello world') }
    } {}
  }
  foreach k $seq2 {
    do_test 1.2.$tn.2.$k {
      execsql { DELETE FROM x1 WHERE k=$k }
    } {}
  }

  catch { array unset V }
  foreach i $seq  { set V($i) 1 }
  foreach i $seq2 { catch { unset V($i) } }
  do_execsql_test 1.2.$tn.3 {
    SELECT k FROM x1 ORDER BY k
  } [lsort -integer [array names V]]
}

#-------------------------------------------------------------------------

set nTest 1
set nRow 10

expr srand(0)

for {set i 0} {$i < $nTest} {incr i} {
  do_execsql_test 2.$i.0 {
    DROP TABLE IF EXISTS x1;
    DROP TABLE IF EXISTS t1;

    CREATE VIRTUAL TABLE x1 USING hct;
    CREATE TABLE t1(k INTEGER PRIMARY KEY, v BLOB);
  }

  catch { array unset V }
  for {set j 0} {$j < $nRow} {incr j} {
    set k [expr int(rand()*$nRow*2)]
    set n [expr int(rand()*30)]
    set v [string range [string repeat [expr rand()] $n] 1 $n]

    execsql { REPLACE INTO x1 VALUES($k, $v) }
    execsql { REPLACE INTO t1 VALUES($k, $v) }

    set V($k) [expr rand()]
    lappend I $k
  }

  set res [db eval {SELECT * FROM t1 ORDER BY k}]
  do_execsql_test 2.$i.1 {
    SELECT * FROM x1 ORDER BY k;
  } $res

  proc by_V {a b} {
    if {$::V($a)==$::V($b)} { return 0 }
    if {$::V($a)<$::V($b)} { return -1 }
    return 1
  }
  foreach k [lrange [lsort -command by_V [array names V]] 0 [expr $nRow/2]] {
    execsql { DELETE FROM x1 WHERE k = $k }
    execsql { DELETE FROM t1 WHERE k = $k }
    lappend D $k
  }
  #puts "{$I} {$D}"

  set res [db eval {SELECT * FROM t1 ORDER BY k}]
  do_execsql_test 2.$i.2 {
    SELECT * FROM x1 ORDER BY k;
  } $res

  set res [db eval {SELECT * FROM t1 ORDER BY k DESC}]
  do_execsql_test 2.$i.3 {
    SELECT * FROM x1 ORDER BY k DESC;
  } $res
}

#-------------------------------------------------------------------------

do_execsql_test 3.0 {
  CREATE VIRTUAL TABLE c1 USING hct;
  INSERT INTO c1 VALUES(1, 'one');
}

do_execsql_test 3.1 {
  BEGIN;
    INSERT INTO c1 VALUES(2, 'two');
  ROLLBACK;
  SELECT * FROM c1;
} {1 one}

do_execsql_test 3.2 {
  BEGIN;
    REPLACE INTO c1 VALUES(1, 'three');
  ROLLBACK;
  SELECT * FROM c1;
} {1 one}

do_execsql_test 3.3 {
  BEGIN;
    DELETE FROM c1;
  ROLLBACK;
  SELECT * FROM c1;
} {1 one}

do_execsql_test 3.4.0 {
  CREATE VIRTUAL TABLE v1 USING hct;
}

do_execsql_test 3.4.1 {
  BEGIN;
    SAVEPOINT xyz4;
      INSERT INTO v1 VALUES(17, '0.153310934153065') ;
    ROLLBACK TO xyz4;
    SELECT * FROM v1;
  ROLLBACK;
}

do_execsql_test 3.4.2.1 {
  BEGIN;
  SAVEPOINT xyz1 ;
  SAVEPOINT xyz2 ;

  SAVEPOINT xyz3 ;
  INSERT INTO v1 VALUES(11, '0.631645512595608');
  ROLLBACK TO xyz3 ;
  RELEASE xyz3;
}

do_execsql_test 3.4.2.2 {
  INSERT  INTO v1 VALUES(46, '0.602538187337359') ;
  RELEASE xyz2;
  ROLLBACK TO xyz1;
  SELECT * FROM v1;
  COMMIT;
}

do_execsql_test 3.5.1 {
  BEGIN;
  SAVEPOINT xyz2 ;
    INSERT  INTO v1 VALUES(29, '0.602538187337359') ;
  RELEASE xyz2;
  SELECT k FROM v1;
} {29}

do_execsql_test 3.5.2 {
  SAVEPOINT xyz2 ;
  ROLLBACK TO xyz2 ;
  SELECT k FROM v1;
  ROLLBACK;
} {29}

do_execsql_test 3.6.1 {
  BEGIN;
    SAVEPOINT xyz1 ;
    RELEASE xyz1 ;
    INSERT  INTO v1 VALUES(11, '0.602538187337359') ;
    SAVEPOINT xyz1 ;
    ROLLBACK TO xyz1 ;
    DELETE FROM v1 WHERE k=9 ;
    SAVEPOINT xyz2 ;
    DELETE FROM v1 WHERE k=5 ;
    SAVEPOINT xyz3 ;
    SELECT k FROM v1;
  COMMIT
} {11}

#-------------------------------------------------------------------------
reset_db
load_static_extension db hct

set nTest 50
set nStmt 100

do_execsql_test 4.setup {
  DROP TABLE IF EXISTS v1;
  DROP TABLE IF EXISTS t1;
  CREATE TABLE t1(k INTEGER PRIMARY KEY, v BLOB);
  CREATE VIRTUAL TABLE v1 USING hct;
}

for {set ii 0} {$ii < $nTest} {incr ii} {
  set iRoot [expr $ii+1]
  execsql {
    DELETE FROM t1;
    INSERT INTO v1 VALUES('root', $iRoot);
  }

  set iSavepoint 0
  set script [list]
  for {set jj 0} {$jj < $nStmt} {incr jj} {
    set iCase [expr int(rand()*5)]
    switch $iCase {
      0 {
        set k [expr int(rand()*$nStmt)]
        set v [expr rand()]
        execsql "INSERT  INTO v1 VALUES($k, '$v')"
        execsql "REPLACE INTO t1 VALUES($k, '$v')"
        lappend script "INSERT  INTO v1 VALUES($k, '$v')"
      }
      1 {
        set k [expr int(rand()*$nStmt)]
        execsql "DELETE FROM v1 WHERE k=$k"
        execsql "DELETE FROM t1 WHERE k=$k"
        lappend script "DELETE FROM v1 WHERE k=$k"
      }
      2 {
        incr iSavepoint
        execsql "SAVEPOINT xyz$iSavepoint"
        lappend script "SAVEPOINT xyz$iSavepoint"
      }
      3 {
        if {$iSavepoint>0} {
          execsql "ROLLBACK TO xyz$iSavepoint"
          lappend script "ROLLBACK TO xyz$iSavepoint"
        }
      }
      4 {
        if {$iSavepoint>0} {
          execsql "RELEASE xyz$iSavepoint"
          lappend script "RELEASE xyz$iSavepoint"
          incr iSavepoint -1
        }
      }
    }

    do_execsql_test 4.$ii.$jj.1 {
      SELECT * FROM v1 ORDER BY k ASC;
    } [execsql {SELECT * FROM t1 ORDER BY k}]

    do_execsql_test 4.$ii.$jj.2 {
      SELECT * FROM v1 ORDER BY k DESC;
    } [execsql {SELECT * FROM t1 ORDER BY k DESC}]
  }

  #foreach s $script { puts "$s ;" }

  while {$iSavepoint>0} {
    execsql "RELEASE xyz$iSavepoint"
    incr iSavepoint -1
  }
}


finish_test

