# 2020 November 5
#
# The author disclaims copyright to this source code.  In place of
# a legal notice, here is a blessing:
#
#    May you do good and not evil.
#    May you find forgiveness for yourself and forgive others.
#    May you share freely, never taking more than you give.
#
#*************************************************************************
# This file implements regression tests for SQLite library.
#

set testdir [file dirname $argv0]
source $testdir/tester.tcl
set testprefix hct_misc2

do_execsql_test 1.0 {
  CREATE TABLE tx(a INTEGER PRIMARY KEY, b TEXT);
}

set nRow 10000

expr srand(0)
unset -nocomplain data

for {set i 0} {$i < $nRow} {incr i} {
  set a  [expr int(rand()*100000)]
  set nb [expr int(rand()*100)]
  set txt [db one { SELECT hex(randomblob($nb)) }]
  execsql { REPLACE INTO tx VALUES($a, $txt) }
  set data($a) $txt
}

foreach k [array names data] {
  do_execsql_test 1.1.$k {
    SELECT a, b FROM tx WHERE a=$k
  } [list $k $data($k)]
}

while {[llength [array names data]]>0} {
  set L [array names data]
  set i [expr int(rand()*[llength $L])]
  set k [lindex $L $i]
  execsql { DELETE FROM tx WHERE a = $k }
  unset data($k)
  set nLength [llength $L]
  if {($nLength % 100)==0} {
    do_execsql_test 1.2.[llength $L] {
      SELECT a FROM tx ORDER BY a
    } [lsort -integer [array names data]]
  }
}

do_execsql_test 1.x {
  SELECT * FROM tx
} {}

finish_test

